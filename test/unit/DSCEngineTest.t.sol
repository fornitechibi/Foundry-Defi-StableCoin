// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {Test, console} from "forge-std/Test.sol";
import {DecentralizedStableCoin} from "../../src/DecentralizedStableCoin.sol";
import {DeployDSC} from "../../script/DeployDSC.s.sol";
import {DSCEngine} from "../../src/DSCEngine.sol";
import {HelperConfig} from "../../script/HelperConfig.s.sol";
import {ERC20Mock} from "@openzeppelin/contracts/mocks/token/ERC20Mock.sol";
import {MockV3Aggregator} from "../mocks/MockV3Aggregator.sol";

contract DSCEngineTest is Test {
    DeployDSC deployer;
    DecentralizedStableCoin dsc;
    HelperConfig config;
    address ethUsdPriceFeed;
    address btcUsdPriceFeed;
    address weth;
    address wbtc;
    DSCEngine dsce;

    address public USER = makeAddr("user");
    address public OWNER = makeAddr("owner");
    uint256 public constant AMOUNT_COLLATERAL = 10 ether;
    uint256 public constant STRATING_ERC20_BALANCE = 10 ether;
    uint256 public constant AMOUNT_TO_MINT = 100 ether;

    function setUp() public {
        deployer = new DeployDSC();
        (dsc, dsce, config) = deployer.run();
        (ethUsdPriceFeed, btcUsdPriceFeed, weth, wbtc,) = config.activeNetworkConfig();

        ERC20Mock(weth).mint(USER, STRATING_ERC20_BALANCE);
    }

    /////////////////////////
    //  Constructor Tests  //
    /////////////////////////

    address[] public tokenAddress;
    address[] public priceFeedAddress;

    function testRevertsIfTokenLengthDoesntMatchPriceFeeds() public {
        tokenAddress.push(weth);
        priceFeedAddress.push(ethUsdPriceFeed);
        priceFeedAddress.push(btcUsdPriceFeed);
        vm.expectRevert(DSCEngine.DSCEngine__tokenAddressessAndPriceFeedAddressessMustBeSameLength.selector);
        new DSCEngine(tokenAddress, priceFeedAddress, address(dsc));
    }

    ///////////////////
    //  Price Tests  //
    ///////////////////

    function testGetUsdValue() public view {
        if (block.chainid != 11155111) {
            uint256 ethAmount = 15e18;
            // 15 Eth * 2000 $ = 30,000e18
            uint256 expectedUsd = 30000e18;
            uint256 actualUSd = dsce.getUsdValue(weth, ethAmount);
            assertEq(expectedUsd, actualUSd);
        }
    }

    function testGetTokenAmountFromUsd() public view {
        uint256 usdAmount = 100 ether;
        uint256 expectedWeth = 0.05 ether;
        uint256 actualWeth = dsce.getTokenAmountFromUsd(weth, usdAmount);
        assertEq(expectedWeth, actualWeth);
    }

    function testGetUsdValueForSepolia() public view {
        if (block.chainid == 11155111) {
            uint256 ethAmount = 15e18;
            // got the expected value from running the getpricefeed in remix in sepolia testnet
            uint256 expectedUsd = 53862086718900000000000;
            uint256 actualUSd = dsce.getUsdValue(weth, ethAmount);
            assertEq(expectedUsd, actualUSd);
        }
    }

    ///////////////////////////////
    //  DepositCollateral Tests  //
    ///////////////////////////////

    function testRevertIfCollateralIsZero() public {
        vm.startPrank(USER);
        ERC20Mock(weth).approve(address(dsce), AMOUNT_COLLATERAL);
        vm.expectRevert(DSCEngine.DSCEngine__NeedMoreThanZero.selector);
        dsce.depositCollateral(weth, 0);
        vm.stopPrank();
    }

    function testRevertIfUnapprovedCollateral() public {
        ERC20Mock ranToken;
        vm.startPrank(USER);
        vm.expectRevert(DSCEngine.DSCEngine__NotAllowedToken.selector);
        dsce.depositCollateral(address(ranToken), AMOUNT_COLLATERAL);
        vm.stopPrank();
    }

    modifier deposistedCollateral() {
        vm.startPrank(USER);
        ERC20Mock(weth).approve(address(dsce), AMOUNT_COLLATERAL);
        dsce.depositCollateral(weth, AMOUNT_COLLATERAL);
        vm.stopPrank();
        _;
    }

    function testCanDepositCollateralAndGetAccountInfo() public deposistedCollateral {
        (uint256 totalDscMinted, uint256 CollateralValueInUsd) = dsce.getAccountInformation(USER);
        uint256 expectedTotalDscMinted = 0;
        uint256 expectedDepositAmount = dsce.getTokenAmountFromUsd(weth, CollateralValueInUsd);
        assertEq(totalDscMinted, expectedTotalDscMinted);
        assertEq(AMOUNT_COLLATERAL, expectedDepositAmount);
    }

    function testCanDepositCollateralWithoutMinting() public view {
        uint256 userBalance = dsc.balanceOf(USER);
        assertEq(userBalance, 0);
    }

    /////////////////////////////////////////
    //  DepositCollateralAndMintDsc Tests  //
    /////////////////////////////////////////

    function testRevertsIfMintedDscBreaksHealthFactor() public {
        (, int256 price,,,) = MockV3Aggregator(ethUsdPriceFeed).latestRoundData();
        uint256 amountToMint =
            (AMOUNT_COLLATERAL * (uint256(price)) * dsce.getAdditionalFeedPrecision()) / dsce.getPrecision();
        vm.startPrank(USER);
        ERC20Mock(weth).approve(address(dsce), AMOUNT_COLLATERAL);
        uint256 expectedHeathFactor =
            dsce.calculateHealthFactor(amountToMint, dsce.getUsdValue(weth, AMOUNT_COLLATERAL));
        console.log("The health factor is: %e", expectedHeathFactor);
        vm.expectRevert(abi.encodePacked(DSCEngine.DSCEngine__BreaksHealthFactor.selector, expectedHeathFactor));
        dsce.depositCollateralAndMintDsc(weth, AMOUNT_COLLATERAL, amountToMint);
        vm.stopPrank();
    }

    modifier deposistedCollateralAndMintDsc() {
        vm.startPrank(USER);
        ERC20Mock(weth).approve(address(dsce), AMOUNT_COLLATERAL);
        dsce.depositCollateralAndMintDsc(weth, AMOUNT_COLLATERAL, AMOUNT_TO_MINT);
        vm.stopPrank();
        _;
    }

    function testCanMintDscWithDepositedCollateral() public deposistedCollateralAndMintDsc {
        uint256 userBalance = dsc.balanceOf(USER);
        (uint256 totalDscMinted,) = dsce.getAccountInformation(USER);
        console.log("The Total dsc minted by the user: %e", totalDscMinted);
        assertEq(userBalance, AMOUNT_TO_MINT);
    }

    /////////////////////
    //  MintDsc Tests  //
    /////////////////////

    function testRevertsIfMintAmountIsZero() public {
        vm.startPrank(USER);
        ERC20Mock(weth).approve(address(dsce), AMOUNT_COLLATERAL);
        vm.expectRevert(DSCEngine.DSCEngine__NeedMoreThanZero.selector);
        dsce.mintDsc(0);
        vm.stopPrank();
    }

    /////////////////////
    //  BurnDsc Tests  //
    /////////////////////

    function testRevertsIfBurnDscAmountIsZero() public deposistedCollateralAndMintDsc {
        vm.expectRevert(DSCEngine.DSCEngine__NeedMoreThanZero.selector);
        dsce.burnDsc(0);
    }
}
